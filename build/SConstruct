#This is a scons MakeFile.
#Compiles and installs the FastNet package.

#Author: Rodrigo Coura Torres (Rodrigo.Torres@cern.ch)

import os

import sc_globals
import sc_libs
import sc_matlab

#Setting the list of directories where to look for header files.
env = Environment(CPPPATH = sc_globals.incPath, ENV = os.environ)

#Creating the Matlab environment builder.
matBuilder = Builder(generator = sc_matlab.matlabBuild, suffix = sc_matlab.getMatlabSuffix())
env.Append(BUILDERS = {'Matlab' : matBuilder})

### Creating the dynamic libraries.
libInstList = [];
libMap = {}
for lib, opt in sc_libs.libs.iteritems():
  libName = env.SharedLibrary(target = lib, source = sc_globals.getSourceFiles('../src/%s' % lib),
                              CCFLAGS = sc_globals.genCPPFlags,
                              LIBS = opt['LIBS'],
                              LIBPATH = sc_globals.libPath)
  libInstList.append(libName)
  libMap[lib] = libName


### Creating Matlab bindings
matBinList = []
matHelpList = []
for mat, opt in sc_matlab.matlab.iteritems():
  matBinding = env.Matlab(target = mat, source = '../src/matlab/%s.cxx' % mat, 
                          CCFLAGS = sc_globals.genCPPFlags, 
                          LIBS = opt['LIBS'], 
                          LIBPATH = sc_globals.libPath)
  matBinList.append(matBinding);
  matHelpList.append('../src/matlab/%s.m' % mat)


###Specifying the installations directories.
libInstDir = '../lib'
matInstDir = '../script/matlab'

#Associating the files list to their installation directories.
env.Install(libInstDir, libInstList)
env.Install(matInstDir, matBinList + matHelpList)

#Creating the installation aliases.
instMat = env.Alias('install-matlab', [libInstDir, matInstDir])
env.Alias('install', [instMat])
